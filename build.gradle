plugins {
    id 'java-library'
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'dev.architectury.loom' version '1.1-SNAPSHOT' apply false
    id "io.github.pacifistmc.forgix" version "1.2.6"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'

    dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
        implementation 'com.google.code.findbugs:jsr305:3.0.2'
    }

    build.finalizedBy(mergeJars)
    assemble.finalizedBy(mergeJars)
}


def getAppVersion = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--abbrev=0'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (Exception e) {
        println "Warning: ${e.message}, is git installed?"
        return "dev"
    }
} as Object

allprojects {
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    repositories {
        maven {
            url "https://cursemaven.com"
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    group = rootProject.group
    description = rootProject.displayname
    archivesBaseName = rootProject.name
    version = getAppVersion()

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }

    publishing {
        repositories {
            maven {
                name = "reposiliteRepository"
                url = uri("https://maven.conczin.net/Artifacts")
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_TOKEN")
                }
            }
        }
    }
}

forgix {
    group = rootProject.group
    mergedJarName = "${rootProject.name}-${rootProject.version}-universal.jar"
    outputDir = "build/libs"

    forge {
        jarLocation = "build/libs/${rootProject.name}-${rootProject.version}-forge.jar"
        mixin "mca.mixin.json"
    }

    fabric {
        jarLocation = "build/libs/${rootProject.name}-${rootProject.version}-fabric.jar"
    }

    quilt {
        jarLocation = "build/libs/${rootProject.name}-${rootProject.version}-quilt.jar"
    }
}